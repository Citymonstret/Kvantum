buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
            jcenter()
        }
    }
    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.1")
        classpath 'com.netflix.nebula:gradle-lint-plugin:latest.release'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.3.1'
}

allprojects {
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'signing'
    apply plugin: 'nebula.lint'

    gradleLint.rules = ['all-dependency']

    license {
        header rootProject.file('HEADER')
        mapping 'java', 'SLASHSTAR_STYLE'
        ext.year = 2018
    }
    repositories {
        maven { url "https://jitpack.io" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testCompile(
                'org.junit.jupiter:junit-jupiter-engine:5.0.1',
                'org.junit.vintage:junit-vintage-engine:4.12.1',
                'org.junit.platform:junit-platform-launcher:1.0.1',
                'org.junit.platform:junit-platform-runner:1.0.1'
        )
    }

    task createPom << {
        pom {
            project {
                inceptionYear '2017'
                packaging 'jar'
                name 'Kvantum'
                description 'Java HTTP(S) server'
                url 'http://kvantum.xyz'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'scm:git:https://github.com/IntellectualSites/Kvantum.git'
                    developerConnection 'scm:git:git@github.com:IntellectualSites/Kvantum.git'
                    url 'https://github.com/IntellectualSites/Kvantum'
                }
                repositories {
                    repository {
                        id 'jitpack'
                        url 'https://jitpack.io'
                    }
                }
                developers {
                    developer {
                        id 'Sauilitired'
                        name 'Alexander SÃ¶derberg'
                        email 'sauilitired@gmail.com'
                        roles {
                            role 'owner'
                        }
                        timezone '+1'
                    }
                }
            }
        }.writeTo('pom.xml')
    }

    build.dependsOn createPom

    if (project.hasProperty('sign')) {
        signing {
            sign configurations.archives
        }
    }
}

group 'xyz.kvantum'
version = '1.2-SNAPSHOT'

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    if (project.hasProperty('full')) {
        artifacts {
            archives sourcesJar
            archives javadocJar
        }
        build.dependsOn sourcesJar
        build.dependsOn javadocJar
    }

    dependencies {
        compile group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'
    }

    configurations.all {
        exclude group: 'io.netty', module: 'netty'
        exclude group: 'org.jboss.netty', module: 'netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-core'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-jobclient'
        exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs'
        exclude group: 'tomcat', module: 'servlet-api'
        exclude group: 'net.sf.cglib', module: 'cglib-nodep'
        exclude group: 'jcuda', module: 'jcuda'

    }
}

description = """"""

task writeVersionToFile << {
    File file = new File(projectDir, "version.txt")
    new File(projectDir, "old-version.txt").text = file.text
    file.text = version
}
