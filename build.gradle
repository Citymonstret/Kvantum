buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
            jcenter()
        }
    }
    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.1")
    }
}

plugins {
    id "io.freefair.lombok" version "3.6.4"
}

allprojects {
    apply plugin: "io.freefair.lombok"
    apply plugin: 'java'
    apply plugin: 'com.github.hierynomus.license'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'signing'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    license {
        header rootProject.file('HEADER')
        mapping 'java', 'SLASHSTAR_STYLE'
        ext.year = 2019
    }

    repositories {
        maven { url "https://jitpack.io" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile(
                'org.junit.jupiter:junit-jupiter-engine:5.7.1',
                'org.junit.vintage:junit-vintage-engine:5.3.2',
                'org.junit.platform:junit-platform-launcher:1.3.2',
                'org.junit.platform:junit-platform-runner:1.3.2'
        )
    }

    task createPom {
        pom {
            project {
                inceptionYear '2017'
                packaging 'jar'
                name 'Kvantum'
                description 'Java HTTP(S) server'
                url 'http://kvantum.xyz'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm {
                    connection 'scm:git:https://github.com/IntellectualSites/Kvantum.git'
                    developerConnection 'scm:git:git@github.com:IntellectualSites/Kvantum.git'
                    url 'https://github.com/IntellectualSites/Kvantum'
                }
                repositories {
                    repository {
                        id 'jitpack'
                        url 'https://jitpack.io'
                    }
                }
                developers {
                    developer {
                        id 'Sauilitired'
                        name 'Alexander SÃ¶derberg'
                        email 'sauilitired@gmail.com'
                        roles {
                            role 'owner'
                        }
                        timezone '+1'
                    }
                }
            }
        }.writeTo('pom.xml')
    }

    build.dependsOn createPom

    if (project.hasProperty('sign')) {
        signing {
            sign configurations.archives
        }
    }
}

group 'xyz.kvantum'
version = '1.3-SNAPSHOT'

subprojects {
    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    if (project.hasProperty('full')) {
        artifacts {
            archives sourcesJar
            archives javadocJar
        }
        build.dependsOn sourcesJar
        build.dependsOn javadocJar
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.8'
        annotationProcessor 'org.projectlombok:lombok:1.18.6'

        compile group: 'net.sourceforge.findbugs', name: 'annotations', version: '1.3.2'
    }

    configurations.all {
        exclude group: 'io.netty', module: 'netty'
        exclude group: 'org.jboss.netty', module: 'netty'
        exclude group: 'org.apache.hadoop', module: 'hadoop-annotations'
        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
        exclude group: 'org.apache.hadoop', module: 'hadoop-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-auth'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-common'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-core'
        exclude group: 'org.apache.hadoop', module: 'hadoop-mapreduce-client-jobclient'
        exclude group: 'org.apache.hadoop', module: 'hadoop-hdfs'
        exclude group: 'tomcat', module: 'servlet-api'
        exclude group: 'net.sf.cglib', module: 'cglib-nodep'
        exclude group: 'jcuda', module: 'jcuda'
        exclude group: 'com.moandjiezana.toml', module: 'toml'
    }
}

description = """"""
