buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
mainClassName = 'xyz.kvantum.server.implementation.KvantumMain'

// Disable ZIP artifacts
tasks.distZip.enabled = false
tasks.shadowDistZip.enabled = false
configurations.archives.artifacts.removeAll { it.type == 'zip' }

// Disabke TAR artifacts
tasks.distTar.enabled = false
tasks.shadowDistTar.enabled = false
configurations.archives.artifacts.removeAll { it.type == 'tar' }

jar {
    manifest {
        attributes 'Implementation-Title': 'Kvantum',
                'Implementation-Version': rootProject.version,
                'Main-Class': 'xyz.kvantum.server.implementation.KvantumMain'
    }
}

artifacts {
    jar
    shadowJar
    sourcesJar
    javadocJar
}

dependencies {
    // AsyncBufferedOutputStream
    compile(group: 'de.jungblut.common', name: 'thomasjungblut-common', version: '1.1') {
        transitive = false
        exclude group: '*'
    }
    // Powers the MongoDB implementation
    compile 'org.mongodb:mongo-java-driver:3.6.1'
    compile 'org.mongodb.morphia:morphia:1.3.2'
    // Cache manager
    compile 'com.github.ben-manes.caffeine:caffeine:2.6.2'
    // HikariCP, SQL connection pooling
    compile 'com.zaxxer:HikariCP:3.2.0'
    // SQLite Driver
    runtime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.25.2'
    // MySQL Driver
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.46'
    // Program argument parser
    compile group: 'com.beust', name: 'jcommander', version: '1.72'
    // Netty
    compile group: 'io.netty', name: 'netty-all', version: '4.1.31.Final'
    // Netty native transports
    compile group: 'io.netty', name: 'netty-transport-native-epoll', version: '4.1.31.Final',
            classifier: 'linux-x86_64'
    //
    // Modules
    //
    compile project(":ServerAPI")
}
